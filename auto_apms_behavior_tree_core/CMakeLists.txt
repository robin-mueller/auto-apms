cmake_minimum_required(VERSION 3.12)
project(auto_apms_behavior_tree_core)

if(CMAKE_COMPILER_IS_GNUCXX OR CMAKE_CXX_COMPILER_ID MATCHES "Clang")
    add_compile_options(-Wall -Wextra -Wpedantic)
endif()

find_package(ament_cmake REQUIRED)
find_package(rclcpp REQUIRED)
find_package(rclcpp_action REQUIRED)
find_package(tinyxml2_vendor REQUIRED)
find_package(behaviortree_cpp REQUIRED)
find_package(auto_apms_util REQUIRED)

# Configure and include BehaviorTree.CPP library
# include(FetchContent)
# FetchContent_Declare(behaviortree_cpp
#     GIT_REPOSITORY https://github.com/BehaviorTree/BehaviorTree.CPP.git
#     GIT_TAG 4.6.2
#     GIT_SHALLOW true
#     GIT_PROGRESS true
# )
# set(BTCPP_BUILD_TOOLS       OFF CACHE BOOL "")
# set(BTCPP_EXAMPLES          OFF CACHE BOOL "")
# set(BTCPP_UNIT_TESTS        OFF CACHE BOOL "")
# set(BTCPP_GROOT_INTERFACE   ON  CACHE BOOL "")
# set(BTCPP_SQLITE_LOGGING    OFF CACHE BOOL "")
# FetchContent_MakeAvailable(behaviortree_cpp)

#
# ------------ Configuration --------------
#

option(AUTO_APMS_BEHAVIOR_TREE_CORE_DEBUG_LOGGING "Enable debug logging severity" ON)
if(AUTO_APMS_BEHAVIOR_TREE_CORE_DEBUG_LOGGING)
    add_compile_definitions(_AUTO_APMS_DEBUG_LOGGING)
endif()

# Core resources
set(_AUTO_APMS_BEHAVIOR_TREE_CORE__NODE_MANIFEST_PARAM_CLASS "class_name")
set(_AUTO_APMS_BEHAVIOR_TREE_CORE__NODE_MANIFEST_PARAM_PORT "port")
set(_AUTO_APMS_BEHAVIOR_TREE_CORE__NODE_MANIFEST_PARAM_WAIT_TIMEOUT "wait_timeout")
set(_AUTO_APMS_BEHAVIOR_TREE_CORE__NODE_MANIFEST_PARAM_REQUEST_TIMEOUT "request_timeout")
set(_AUTO_APMS_BEHAVIOR_TREE_CORE__RESOURCE_TYPE_NAME__TREE "auto_apms_behavior_tree_core__tree")

#
# ------------ Library --------------
#

add_library(auto_apms_behavior_tree_core SHARED
    "src/node/node_registration_params.cpp"
    "src/node/node_manifest.cpp"
    "src/node/ros_node_context.cpp"
    "src/resource/tree_resource.cpp"
    "src/resource/node_registration_loader.cpp"
)
target_include_directories(auto_apms_behavior_tree_core PUBLIC
    $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/include>
    $<INSTALL_INTERFACE:include>
)
target_compile_definitions(auto_apms_behavior_tree_core PRIVATE
    _AUTO_APMS_BEHAVIOR_TREE_CORE__RESOURCE_TYPE_NAME__TREE="${_AUTO_APMS_BEHAVIOR_TREE_CORE__RESOURCE_TYPE_NAME__TREE}"
    _AUTO_APMS_BEHAVIOR_TREE_CORE__NODE_MANIFEST_PARAM_CLASS="${_AUTO_APMS_BEHAVIOR_TREE_CORE__NODE_MANIFEST_PARAM_CLASS}"
    _AUTO_APMS_BEHAVIOR_TREE_CORE__NODE_MANIFEST_PARAM_PORT="${_AUTO_APMS_BEHAVIOR_TREE_CORE__NODE_MANIFEST_PARAM_PORT}"
    _AUTO_APMS_BEHAVIOR_TREE_CORE__NODE_MANIFEST_PARAM_WAIT_TIMEOUT="${_AUTO_APMS_BEHAVIOR_TREE_CORE__NODE_MANIFEST_PARAM_WAIT_TIMEOUT}"
    _AUTO_APMS_BEHAVIOR_TREE_CORE__NODE_MANIFEST_PARAM_REQUEST_TIMEOUT="${_AUTO_APMS_BEHAVIOR_TREE_CORE__NODE_MANIFEST_PARAM_REQUEST_TIMEOUT}"
)
ament_target_dependencies(auto_apms_behavior_tree_core
    rclcpp
    rclcpp_action
    TinyXML2
    behaviortree_cpp
    auto_apms_util
)
# target_link_libraries(auto_apms_behavior_tree_core
#     behaviortree_cpp
# )

# Install the version header to the autogenerated headers dir also used by rosidl generators
ament_generate_version_header(auto_apms_behavior_tree_core HEADER_PATH "${PROJECT_NAME}/version.hpp")

#
# ------------ Utility Executables --------------
#

# Merge multiple node manifest files to a single one and collect the shared library paths for the required nodes
add_executable(create_node_manifest "src/cli/create_node_manifest.cpp")
target_link_libraries(create_node_manifest auto_apms_behavior_tree_core)

# Generate the node model according to a node manifest
add_executable(create_node_model "src/cli/create_node_model.cpp")
target_link_libraries(create_node_model auto_apms_behavior_tree_core)

#
# ------------ Packaging --------------
#

###### Export libraries ######
install(
    TARGETS
    auto_apms_behavior_tree_core
    EXPORT export_${PROJECT_NAME}
    LIBRARY DESTINATION lib
    ARCHIVE DESTINATION lib
    RUNTIME DESTINATION bin
)
ament_export_targets(export_${PROJECT_NAME} HAS_LIBRARY_TARGET)
ament_export_dependencies(
    rclcpp
    rclcpp_action
    TinyXML2
    behaviortree_cpp
    auto_apms_util
)

install(
    TARGETS
    create_node_manifest
    create_node_model
    DESTINATION "lib/${PROJECT_NAME}"
)

install(
    DIRECTORY "include/${PROJECT_NAME}"
    DESTINATION "include"
)

install(
    DIRECTORY "cmake"
    DESTINATION "share/${PROJECT_NAME}"
)

ament_package(CONFIG_EXTRAS "auto_apms_behavior_tree_core-extras.cmake.in")
