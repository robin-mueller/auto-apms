cmake_minimum_required(VERSION 3.12)
project(auto_apms_util)

if(CMAKE_COMPILER_IS_GNUCXX OR CMAKE_CXX_COMPILER_ID MATCHES "Clang")
    add_compile_options(-Wall -Wextra -Wpedantic)
endif()

find_package(ament_cmake REQUIRED)
find_package(rclcpp REQUIRED)
find_package(rcpputils REQUIRED)
find_package(rclcpp_action REQUIRED)
find_package(ament_index_cpp REQUIRED)
find_package(generate_parameter_library REQUIRED)
find_package(pluginlib REQUIRED)
find_package(yaml_cpp_vendor REQUIRED)

option(AUTO_APMS_UTIL_DEBUG_LOGGING "Enable debug logging severity" OFF)
if(AUTO_APMS_UTIL_DEBUG_LOGGING)
    add_compile_definitions(_AUTO_APMS_DEBUG_LOGGING)
endif()

# ActionWrapper parameter names
set(_AUTO_APMS_UTIL__ACTION_PARAM_LOOP_RATE "loop_rate")
set(_AUTO_APMS_UTIL__ACTION_PARAM_FEEDBACK_RATE "feedback_rate")

# Plugin resource type name according to pluginlib convention.
# We don't rely on pluginlib's resource parsing, but it's nice to stick to conventions anyways.
set(_AUTO_APMS_UTIL__RESOURCE_TYPE_NAME__PLUGINLIB "auto_apms_util__pluginlib__plugins")

#
# ------------ Library --------------
#

# Generate parameter headers employing https://github.com/PickNikRobotics/generate_parameter_library
configure_file("config/action_wrapper_params.yaml.in" "${PROJECT_BINARY_DIR}/${PROJECT_NAME}/action_wrapper_params.yaml")
set(_temp ${CMAKE_CURRENT_SOURCE_DIR})  # WORKAROUND: temporarily change CMAKE_CURRENT_SOURCE_DIR since the function doesn't allow to load the parameter definition file from a different directory
set(CMAKE_CURRENT_SOURCE_DIR "${PROJECT_BINARY_DIR}/${PROJECT_NAME}")
generate_parameter_library(action_wrapper_params "action_wrapper_params.yaml")
set(CMAKE_CURRENT_SOURCE_DIR ${_temp})

add_library(auto_apms_util SHARED
    "src/action_wrapper.cpp"
    "src/string.cpp"
    "src/resource.cpp"
    "src/logging.cpp"
    "src/filesystem.cpp"
)
target_include_directories(auto_apms_util PUBLIC
    $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/include>
    $<INSTALL_INTERFACE:include>
)
target_compile_definitions(auto_apms_util PRIVATE
    _AUTO_APMS_UTIL__ACTION_PARAM_LOOP_RATE="${_AUTO_APMS_UTIL__ACTION_PARAM_LOOP_RATE}"
    _AUTO_APMS_UTIL__ACTION_PARAM_FEEDBACK_RATE="${_AUTO_APMS_UTIL__ACTION_PARAM_FEEDBACK_RATE}"
)
target_compile_definitions(auto_apms_util PUBLIC
    _AUTO_APMS_UTIL__RESOURCE_TYPE_NAME__PLUGINLIB="${_AUTO_APMS_UTIL__RESOURCE_TYPE_NAME__PLUGINLIB}"
)
ament_target_dependencies(auto_apms_util
    rclcpp
    rcpputils
    rclcpp_action
    ament_index_cpp
    pluginlib
    yaml_cpp_vendor
)
target_link_libraries(auto_apms_util
    action_wrapper_params
)

# Install the version header to the autogenerated headers dir also used by rosidl generators
ament_generate_version_header(auto_apms_util HEADER_PATH "${PROJECT_NAME}/version.hpp")

#
# ------------ Testing --------------
#

if(BUILD_TESTING)
    find_package(auto_apms_interfaces REQUIRED)
    find_package(ament_cmake_gtest REQUIRED)

    ament_add_gtest(${PROJECT_NAME}_unit_tests
        "test/unit/main.cpp"
    )
    ament_target_dependencies(${PROJECT_NAME}_unit_tests
        auto_apms_interfaces
    )
    target_link_libraries(${PROJECT_NAME}_unit_tests
        auto_apms_util
    )
endif()

#
# ------------ Packaging --------------
#

install(
    TARGETS
    auto_apms_util
    action_wrapper_params
    EXPORT export_${PROJECT_NAME}
    LIBRARY DESTINATION lib
    ARCHIVE DESTINATION lib
    RUNTIME DESTINATION bin)

install(
    DIRECTORY include/${PROJECT_NAME}
    DESTINATION include)

install(
    DIRECTORY launch
    DESTINATION share/${PROJECT_NAME}
)

install(
    DIRECTORY cmake
    DESTINATION share/${PROJECT_NAME}
)

ament_export_targets(export_${PROJECT_NAME} HAS_LIBRARY_TARGET)
ament_export_dependencies(
    rclcpp
    rcpputils
    rclcpp_action
    ament_index_cpp
    pluginlib
    yaml_cpp_vendor
    generate_parameter_library # TODO Remove if generate_parameter_library 0.4.0 is available
)

ament_package(CONFIG_EXTRAS "auto_apms_util-extras.cmake.in")
