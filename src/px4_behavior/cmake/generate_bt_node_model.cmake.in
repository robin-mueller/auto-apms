set(PX4_BEHAVIOR_CREATE_BT_NODE_MODEL_EXE_PATH @CMAKE_INSTALL_PREFIX@/lib/@PROJECT_NAME@/create_bt_node_model)

# Automatically generate the model of the configured node plugins and install the model file after building.
# Only plugins registered with px4_behavior_add_plugin in this build are considered.
macro(px4_behavior_generate_bt_node_model config_yaml_source_path plugin_dir)

    # Get all plugin targets of this build
    px4_behavior_get_all_bt_plugin_targets(_all_bt_plugin_targets)

    # Set model definition output file path
    get_filename_component(_config_yaml_source_stem ${config_yaml_source_path} NAME_WE)
    set(_model_build_path "${CMAKE_CURRENT_BINARY_DIR}/${_config_yaml_source_stem}_model.xml")

    # Add custom command and target for file generation
    add_custom_command(OUTPUT "${_model_build_path}"
        COMMAND "${PX4_BEHAVIOR_CREATE_BT_NODE_MODEL_EXE_PATH}"  # variable was defined in create_model_definition.cmake
            "${config_yaml_source_path}" # Absolute path of the config source file
            "${_model_build_path}" # Absolute directory to write the model definition file to
            "${plugin_dir}" # Absolute directory where the plugin libraries are stored.
        DEPENDS "${config_yaml_source_path}" ${_all_bt_plugin_targets}
        COMMENT "Executing command to create model definition: ${PX4_BEHAVIOR_CREATE_BT_NODE_MODEL_EXE_PATH}")
    add_custom_target(_target_generate_${_config_yaml_source_stem}_model ALL  # _config_yaml_source_stem must not contain whitespaces!!!
        DEPENDS "${_model_build_path}")

    # Install the generated file to share
    install(
        FILES "${_model_build_path}"
        DESTINATION share/@PX4_BEHAVIOR_SHARE_DIR_NAME@/model)
endmacro()
